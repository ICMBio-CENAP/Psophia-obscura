

model {
  
  psi ~ dunif(0,1)
  tau.gam ~ dgamma(.1,.1)
  tau.phi ~ dgamma(.1,.1)
  taup ~ dgamma(.1,.1)
  sigma.phi <- 1/sqrt(tau.phi)
  sigma.gam <- 1/sqrt(tau.gam)
  sigma.p <- sqrt(1/taup)
  
  mup.prob[1] ~ dunif(0,1)
  logit(mup[1]) <- mup.prob[1]
  for(i in 2:nyear){
    mup.prob[i] ~ dunif(0,1)
    logit(mup[i]) <- mup.prob[i]
    muphi.prob[i] ~ dunif(0,1)
    logit(muphi[i]) <- muphi.prob[i]
    mugam.prob[i] ~ dunif(0,1)
    logit(mugam[i]) <- mugam.prob[i]
  }
  
  for(i in 1:nsite){
    lp[i] ~ dnorm(0,taup)I(-12,12)
    for(t in 1:nyear){
      logit(p[i,t])<-mup[t]+lp[i]
    }
  }
  
  for(i in 1:nsite){
    z[i,1] ~ dbern(psi)
    lphi[i] ~ dnorm(0,tau.phi)I(-12,12)
    lgam[i] ~ dnorm(0,tau.gam)I(-12,12)
    for(t in 2:nyear){
      logit(gamma[i,t]) <- mugam[t] + lgam[i] 
      logit(phi[i,t]) <- muphi[t] + lphi[i] 
      muZ[i,t] <- z[i,t-1]*phi[i,t] + (1-z[i,t-1])*gamma[i,t]
      z[i,t] ~ dbern(muZ[i,t])
    }
  }
  
  for(i in 1:nsite){
    for (t in 1:nyear){
      Px[i,t] <- z[i,t]*p[i,t]
      x[i,t] ~ dbin(Px[i,t],50)
    }
  }
  
  for(i in 1:nyear){
    psi.year[i] <- sum(z[1:nsite,i])
  }
  for(i in 2:nyear){
    growthr[i-1] <- psi.year[i]/psi.year[i-1]
  }

}

