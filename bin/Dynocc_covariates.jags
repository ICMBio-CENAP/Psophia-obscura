
model {

  ## Priors
  
  #for (k in 1:(nyear-1)){
  #  phi[k] ~ dunif(0, 1)
  #  gamma[k] ~ dunif(0, 1)
  #}

  # random block effects
  for (i in 1:nblock){
      mu.psi[i] ~ dnorm(0, tau.psi)
      #mu.p[i] ~ dnorm(0, tau.p)
      mu.phi[i] ~ dnorm(0, tau.phi)
      mu.gam[i] ~ dnorm(0, tau.gam)
    } #i
  # hyperpriors
  tau.psi ~ dgamma(0.001,0.001)
  #tau.p ~ dgamma(0.001,0.001)
  tau.phi ~ dgamma(0.001,0.001)
  tau.gam ~ dgamma(0.001,0.001)
  
  # random block and year effects for p
  for (i in 1:nblock){
    for (k in 1:nyear){
    mu.p[i,k] ~ dnorm(0, tau.p)
    } #k
  } #i
  tau.p ~ dgamma(0.001,0.001)
   

  # effects (betas)
  for (j in 1:3) {
      beta.psi[j] ~ dnorm(0, 0.01)
      #beta.p[j] ~ dnorm(0, 0.01)
      beta.phi[j] ~ dnorm(0, 0.01)
      beta.gam[j] ~ dnorm(0, 0.01)
  }

  ## Ecological model: state conditional on parameters
  for (i in 1:nsite){
    logit(psi[i,1]) <- mu.psi[block[i]] + beta.psi[1]*distEdge[i] + beta.psi[2]*basalArea[i] + beta.psi[3]*recovery[i]
    z[i,1] ~ dbern(psi[i,1])
    
    for (k in 2:nyear){
      logit(phi[i,k-1]) <- mu.phi[block[i]] + beta.phi[1]*distEdge[i] + beta.phi[2]*basalArea[i] + beta.phi[3]*recovery[i]
      logit(gamma[i,k-1]) <- mu.gam[block[i]] + beta.gam[1]*distEdge[i] + beta.gam[2]*basalArea[i] + beta.gam[3]*recovery[i]
      
      muZ[i,k] <- z[i,k-1]*phi[i,k-1] + (1-z[i,k-1])*gamma[i,k-1]
      z[i,k] ~ dbern(muZ[i,k])
      } #k
   } #i
   
   ## Observation model
   for (i in 1:nsite){
     for (j in 1:nrep){
       for (k in 1:nyear){
         logit(p[i,j,k]) <- mu.p[block[i],k]
         muy[i,j,k] <- z[i,k]*p[i,j,k]  # can only be detected if z=1
         y[i,j,k] ~ dbern(muy[i,j,k])
         } #k
     } #j
   } #i
   
   # Derived parameters: Sample and population occupancy, growth rate and turnover
   #n.occ[1] <- sum(z[1:nsite,1])
   #  psi[i,1] <- psi[i,1]
   for (i in 1:nsite){
     for (k in 2:nyear){
       psi[i,k] <- psi[i,k-1]*phi[i,k-1] + (1-psi[i,k-1])*gamma[i,k-1]
       #n.occ[k] <- sum(z[1:nsite,k])
       growthr[i,k-1] <- psi[i,k]/psi[i,k-1]  # originally we had growthr[k]. JAGS seem to dislike vectoring going from 2..K.
       turnover[i,k-1] <- (1 - psi[i,k-1]) * gamma[i,k-1]/psi[i,k]
     } # k
    } #i
}

