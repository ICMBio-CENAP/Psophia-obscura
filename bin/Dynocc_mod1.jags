
model {

# Specify priors
#psi1 ~ dunif(0, 1)

for (k in 1:(nyear-1)){
   phi[k] ~ dunif(0, 1)
   gamma[k] ~ dunif(0, 1)
   p[k] ~ dunif(0, 1) 
   }
p[nyear] ~ dunif(0, 1)

# priors for GLM occupancy 1st year
alpha.occ ~ dunif(-10, 10)
beta1.occ ~ dunif(-10, 10) # elevation
beta2.occ ~ dunif(-10, 10) # slope
beta3.occ ~ dunif(-10, 10) # dist.water

# Ecological submodel: Define state conditional on parameters
for (i in 1:nsite){
   z[i,1] ~ dbern(psi1[i])
   logit(psi1[i]) <- alpha.occ + beta1.occ*elevation[i] + beta2.occ*slope[i] + beta3.occ*dist.water[i]
   
   
   for (k in 2:nyear){
      muZ[i,k]<- z[i,k-1]*phi[k-1] + (1-z[i,k-1])*gamma[k-1]
      z[i,k] ~ dbern(muZ[i,k])
      } #k
   } #i

# Observation model
for (i in 1:nsite){
   for (j in 1:nrep){
      for (k in 1:nyear){
         muy[i,j,k] <- z[i,k]*p[k]
         y[i,j,k] ~ dbern(muy[i,j,k])
         } #k
      } #j
   } #i

# Derived parameters: Sample and population occupancy, growth rate and turnover

for (i in 1:nsite){
   psi[i,1] <- psi1[i]
   }

n.occ[1] <- sum(z[1:nsite,1])

for (k in 2:nyear){
   n.occ[k] <- sum(z[1:nsite,k])
for (i in 1:nsite){
   psi[i,k] <- psi[i,k-1]*phi[k-1] + (1-psi[i,k-1])*gamma[k-1]
#   growthr[k-1] <- psi[i,k]/psi[i,k-1]           # originally we had growthr[k]. JAGS seem to dislike vectoring going from 2..K.
#   turnover[k-1] <- (1 - psi[i,k-1]) * gamma[k-1]/psi[i,k]
       }# year
   }# site
}

